# Based on the file created for Arch Linux by:
# Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Bob Fanger < bfanger(at)gmail >
# Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Rob McCathie <rob@manjaro.org>

pkgname=linux44-r8168
_pkgname=r8168
pkgver=8.040.00
pkgrel=13
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux44')
makedepends=('linux44-headers')
provides=("$_pkgname=$pkgver")
groups=('linux44-extramodules')
source=("https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('1f7aad3a066c152837e03e558ba720742e792d281c96c5412cff32d68875415b')
install=$_pkgname.install

build() {
	_extramodules=extramodules-4.4-MANJARO
	_kernver="$(cat /usr/lib/modules/$_extramodules/version || true)"
	cd "$_pkgname-$pkgver"

	# avoid using the Makefile directly -- it doesn't understand
	# any kernel but the current.
	make -C /usr/lib/modules/$_kernver/build \
	SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
	EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
	modules
}

package() {
	_extramodules=extramodules-4.4-MANJARO
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

	# set the kernel we've built for inside the install script
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
