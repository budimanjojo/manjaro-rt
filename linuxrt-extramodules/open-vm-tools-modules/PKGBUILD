# Based on the file created for Arch Linux by:
# Sergej Pupykin <pupykin.s+arch@gmail.com>
# Krzysztof Raczkowski <raczkow@gmail.com>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

pkgname=linux44-open-vm-tools-modules
_pkgname=open-vm-tools-modules
groups=('linux44-extramodules')
epoch=2
pkgver=2013.09.16
_pkgsubver=1328054
pkgrel=13
pkgdesc="kernel modules for the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('GPL')
makedepends=('libdnet' 'procps' 'icu' 'uriparser' 'linux44-headers')
depends=("linux44")
provides=("$_pkgname=$pkgver")
groups=('linux44-extramodules')
install=$_pkgname.install
options=('!strip')
source=("http://downloads.sourceforge.net/open-vm-tools/open-vm-tools-$pkgver-${_pkgsubver}.tar.gz"
	"modprobe.conf"
	"3.10-vmblock.patch"
	"3.10-vmsync.patch"
	"3.16-vmxnet.patch")
sha256sums=('470a6ea3ce14c2c5ea6b7bc59745eccbacc8d88a3f343e712312786435975d13'
            '27b83062b8b6a279e8bfb13f10566467377f38ac077e75933211912cac4a085d'
            'f50679c3087ef770ea8666e9432dd8990344b1b1c3725bf3e492dd6d48da3e4d'
            'e7a3a4115087acd81272a52cf419faef0e1efcc431fc083811bddd3cf3e37327'
            '74218a9dc18faf7d61c900d166d01f1bb19cad174b86dbe7671046de9a03d6f1'
)

build() {
  _extramodules=extramodules-4.4-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  patch -p1 -i "$srcdir/3.10-vmsync.patch"
  patch -p1 -i "$srcdir/3.10-vmblock.patch"
  patch -p2 -i "$srcdir/3.16-vmxnet.patch"
  sed -i 's|putname(name);|__putname(name);|' modules/linux/vmblock/linux/control.c

  [ $NOEXTRACT -eq 1 ] || ./configure \
      --prefix=/usr \
      --without-x \
      --with-linuxdir=/usr/lib/modules/$_kernver \
      --with-kernel-release=$_kernver

  # remove broken module
  sed -i 's|vmxnet vmhgfs|vmxnet|g' modules/Makefile
  make -C modules modules
}

package() {
  _extramodules=extramodules-4.4-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  depends=("linux44")

  cd "$srcdir/open-vm-tools-${pkgver}-${_pkgsubver}"
  mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
  for MOD in `find -type f -name '*.ko'`; do
    install -Dm644 $MOD $pkgdir/usr/lib/modules/${_extramodules}/
  done
  gzip $pkgdir/usr/lib/modules/${_extramodules}/*.ko

  install -D -m 644 ${srcdir}/modprobe.conf ${pkgdir}/etc/modprobe.d/${pkgname}.conf
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "${startdir}/${_pkgname}.install"
}

