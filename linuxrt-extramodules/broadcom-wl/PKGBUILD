# Based on the file created for Arch Linux by: Frank Vanderham

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Guinux <guillaume@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=linuxrt-broadcom-wl
_pkgname=broadcom-wl
pkgver=6.30.223.248
pkgrel=14
pkgdesc='Broadcom 802.11 Linux STA wireless driver BCM43142.'
url='https://bbs.archlinux.org/viewtopic.php?id=145884'
arch=('i686' 'x86_64')
license=('custom')
depends=('linuxrt')
makedepends=("broadcom-wl-dkms>=$pkgver"
             'dkms'
             'linuxrt' 'linuxrt-headers')
groups=('linuxrt-extramodules')
provides=("$_pkgname=$pkgver")
install=install
source=(broadcom-wl-dkms.conf)
sha256sums=('b97bc588420d1542f73279e71975ccb5d81d75e534e7b5717e01d6e6adf6a283')

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf

  # copy this dir to local dir so we can patch it.
  cp -r -L dkms/broadcom-wl/$pkgver/source dkms/broadcom-wl/$pkgver/src
  rm dkms/broadcom-wl/$pkgver/source
  mv dkms/broadcom-wl/$pkgver/src dkms/broadcom-wl/$pkgver/source

  sed -i -e 's|rdtscl(cycles)|cycles = 0|g' dkms/broadcom-wl/$pkgver/source/src/shared/linux_osl.c
}

build() {
  _extraver=extramodules-4.4-rt-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"

  # build host modules
  msg2 'Build module'
  dkms --dkmsframework dkms.conf build "broadcom-wl/$pkgver" -k "$_kernver"
}

package_linuxrt-broadcom-wl(){
  _extraver=extramodules-4.4-rt-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  depends=('linuxrt')

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/broadcom-wl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
  install -D -m 644 "${srcdir}"/broadcom-wl-dkms.conf "${pkgdir}"/etc/modprobe.d/linux44-broadcom-wl.conf
}
