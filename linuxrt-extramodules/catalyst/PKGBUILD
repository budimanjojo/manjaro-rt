# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>


_linuxprefix=linux44
_extramodules=extramodules-4.4-MANJARO
pkgname=$_linuxprefix-catalyst
_pkgname=catalyst
pkgver=15.201.1151
pkgrel=13
epoch=1
_pkgver=15.201.1151
_tarball=15.9-linux-installer-15.201.1151
provides=("$_pkgname=$_pkgver")
groups=("$_linuxprefix-extramodules")
pkgdesc="AMD/ATI Catalyst drivers for linux. fglrx kernel module only."
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
depends=("$_linuxprefix" "catalyst-utils=${epoch}:${pkgver}")
makedepends=("$_linuxprefix-headers" "gcc49")
conflicts=('catalyst-legacy' "$_linuxprefix-catalyst-legacy")
install=catalyst.install

source=(http://www2.ati.com/drivers/linux/amd-catalyst-${_tarball}-x86.x86_64.zip
        #https://launchpad.net/ubuntu/+archive/primary/+files/fglrx-installer_${_tarball}.orig.tar.gz
        ati_make.sh
        lano1106_fglrx_intel_iommu.patch
        lano1106_kcl_agp_13_4.patch
        makefile_compat.patch
        kolasa_fglrx_312.patch
	cold-fglrx-3.14-current_euid.patch
	manjaro_fglrx_40.patch
        kolasa_fglrx_4.1.patch
        manjaro_fglrx_4.2.patch
        manjaro_fglrx_4.3.patch
        gentoo_fglrx_4.3_mtrr.patch
	LICENSE.TXT)

sha256sums=('bf3e6e7d5c51db3d075410a3f116f865b82823debc1d66698d187249feec6a91'
            '8b2adf804c2691d075bb75fbb626be59f3769a2f4f1418554fc3de91a65451ab'
            'd7ab220204976b5802dbfa7cbd05ec0c5a9ff3524a2020b1b8deff4f601a05fa'
            '37344e6949b391a54491bcba68c2239393e442a00114ee36fe25d775d9656520'
            'd344df75ffbe9bd899e288862f41983c2e24b5eff3e7e8cf02d4891eeb137fff'
            '8fe103d8c0ae676d68ea518337024126bcdda7fc7befd57d5d2545e4f160736d'
            '8095031ee68edbb67323a562b54c9cf46aade7c91738950467b91eda68e434ee'
            '72e06fc755f40cb7697c37c4045cca909075e6eccb6260924b94508ec77744f6'
            'e2c51ca60a452b78854a7e7c739dc9ccce4650cde882026cf5e01dd4afb9340c'
            '2bc280abeddef92832997433f3182063a51cd2e580426727e97f887ae8af485f'
            'a7cebf78578c68e6b765c5b821d5c0d4bc716ad8d97456478b60beecd35a7602'
            '2881026d607f242efc4569bd39d647eae4f2e402da06de5b90476a57c349777c'
            '4f7530540f6e1ea032746e94bd164160abdf93c15509bfb8bdbecd23bcea5c47')

prepare() {
	#cd ${srcdir}/fglrx-$_pkgver
	/bin/sh ./AMD-Catalyst-15.9-Linux-installer-${_pkgver}-x86.x86_64.run --extract archive_files
	#/bin/sh ./amd-catalyst-${_pkgver}-linux-x86.x86_64.run --extract archive_files

	# move files
	#mkdir common
	#mv etc lib usr common
	#mkdir -p fglrx-$_pkgver/archive_files
	#mv arch common xpic xpic_64a LICENSE.TXT fglrx-$_pkgver/archive_files
	mkdir -p fglrx-$_pkgver
	mv archive_files fglrx-$_pkgver
}

build() {
	_kernver="$(cat /usr/lib/modules/${_extramodules}/version)"

	## include ati_make.sh and use _ati_check function from it
	cd "${srcdir}"
	. ati_make.sh
	_ati_check

	if [ "${CARCH}" = "x86_64" ]; then
		BUILDARCH=x86_64
		_archdir=x86_64
	fi
	if [ "${CARCH}" = "i686" ]; then
		BUILDARCH=i386
		_archdir=x86
	fi

	cd "${srcdir}/fglrx-$_pkgver/archive_files"

	## makefile patch to choose arch_compat_alloc_user_space or older compat_alloc_user_space
	## works only in combination with ati_make.sh script
	patch -Np1 -i ${srcdir}/makefile_compat.patch
	patch -Np1 -i ${srcdir}/lano1106_fglrx_intel_iommu.patch
	patch -Np1 -i ${srcdir}/lano1106_kcl_agp_13_4.patch
        patch -Np1 -i ${srcdir}/cold-fglrx-3.14-current_euid.patch

	cd "${srcdir}/fglrx-$_pkgver/archive_files/common/lib/modules/fglrx/build_mod"
        patch -Np1 -i ${srcdir}/kolasa_fglrx_312.patch
        patch -Np5 -i ${srcdir}/manjaro_fglrx_40.patch
        patch -Np1 -i ${srcdir}/kolasa_fglrx_4.1.patch
        patch -Np1 -i ${srcdir}/manjaro_fglrx_4.2.patch
        patch -Np1 -i ${srcdir}/manjaro_fglrx_4.3.patch
        patch -Np6 -i ${srcdir}/gentoo_fglrx_4.3_mtrr.patch

	## fix linux44 build
	sed -i -e s'|XSTATE_FP|XFEATURE_MASK_FP|g' firegl_public.c

	cp "${srcdir}/fglrx-$_pkgver/archive_files/arch/${_archdir}/lib/modules/fglrx/build_mod/libfglrx_ip.a" .
	cp 2.6.x/Makefile .

	## Compile module using _ati_check variables
	make CC=gcc-4.9 -C /usr/lib/modules/${_kernver}/build SUBDIRS="`pwd`" ARCH=${BUILDARCH} \
	MODFLAGS="-DMODULE -DATI -DFGL -DPAGE_ATTR_FIX=$PAGE_ATTR_FIX -DCOMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE $def_smp $def_modversions" \
	PAGE_ATTR_FIX=$PAGE_ATTR_FIX COMPAT_ALLOC_USER_SPACE=$COMPAT_ALLOC_USER_SPACE modules
}


package() {
	cd "${srcdir}/fglrx-$_pkgver/archive_files/common/lib/modules/fglrx/build_mod"
	install -m755 -d "${pkgdir}/usr/lib/modules/${_extramodules}/"
	install -m644 fglrx.ko "${pkgdir}/usr/lib/modules/${_extramodules}/"
	find "${pkgdir}" -name '*.ko' -exec gzip -9 {} \;

	## License
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 "${srcdir}/fglrx-$_pkgver/archive_files/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}"

	## Install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/" $startdir/*.install
	sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/" $startdir/*.install
}
