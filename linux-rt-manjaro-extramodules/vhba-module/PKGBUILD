# Based on the file created for Arch Linux by:
# Ray Rashif <schiv@archlinux.org>
# Mateusz Herych <heniekk@gmail.com>
# Charles Lindsay <charles@chaoslizard.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

_linuxprefix=linux-rt-manjaro
_extramodules=extramodules-4.6-rt-MANJARO
pkgname=$_linuxprefix-vhba-module
_pkgname=vhba-module
pkgver=20140629
pkgrel=9
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=("$_linuxprefix")
makedepends=("$_linuxprefix-headers")
provides=("$_pkgname=$pkgver")
groups=("$_linuxprefix-extramodules")
install=$_pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$_pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
sha256sums=('8e607cab56038ef4f5923c57279bbee4c2c0b57ce77e7003693d0938d09d6957'
         '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  
  cd "$srcdir/$_pkgname-$pkgver"

  make -j1 KDIR=/usr/lib/modules/${_kernver}/build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/usr/lib/modules/$_extramodules/vhba.ko"

  sed -i "s/EXTRAMODULES=.*/EXTRAMODULES=$_extramodules/" \
    "$startdir/vhba-module.install"

  find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;

  install -Dm644 "$srcdir/60-vhba.rules" \
          "$pkgdir/usr/lib/udev/rules.d/60-$_linuxprefix-vhba.rules"
}

# vim:set ts=2 sw=2 et:
