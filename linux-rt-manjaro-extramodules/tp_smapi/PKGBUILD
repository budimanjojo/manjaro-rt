# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: xduugu
# Contributor: nh2
# Contributor: Steven Davidovitz <steviedizzle ð gmail đ com>
# Contributor: Nick B <Shirakawasuna ð gmail đ com>
# Contributor: Christof Musik <christof ð senfdax đ de>
# Contributor: Stefan Rupp <archlinux ð stefanrupp đ de>
# Contributor: Ignas Anikevicius <anikevicius ð gmail đ com>

_linuxprefix=linux-rt-manjaro
_extraver=extramodules-4.6-rt-MANJARO
_pkgname=tp_smapi
pkgname=$_linuxprefix-tp_smapi
pkgver=0.41
pkgrel=8
pkgdesc="Modules for ThinkPad's SMAPI functionality"
arch=(i686 x86_64)
url='https://github.com/evgeni/tp_smapi'
license=('GPL')
depends=("$_linuxprefix")
makedepends=("$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
install="${_pkgname}.install"
source=("https://github.com/downloads/evgeni/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "change_type_of_force_io_to_bool.patch::https://github.com/evgeni/tp_smapi/commit/8f40fcf3d9a4bfb9c3dae1caa2107ea3a367a609.patch")
md5sums=('63c683415c764568f6bf17c7eabe4752'
         'fd46e3ac1b0e2ab7d0614531a693ccae')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  patch -Np1 -i "../change_type_of_force_io_to_bool.patch"
}

build() {
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules")

  cd "${srcdir}/${_pkgname}-${pkgver}"

  sed -ri -e "s/^(KVER\s*:=).*$/\1 ${_kernver}/" \
          -e "s#^(KBASE\s*:=).*\$#\1 ${_extramodules%/*}/${_kernver}#" Makefile
  make HDAPS=1
}

package() {
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _extramodules=$(readlink -f "/usr/lib/modules/${_kernver}/extramodules")

  make -C "${_extramodules%/*}/${_kernver}/build" \
    INSTALL_MOD_PATH="${pkgdir}/${_extramodules%%/lib/*}" \
    M="${srcdir}/${_pkgname}-${pkgver}" modules_install

  cd "${pkgdir}/${_extramodules%/*}"
  mv "${_kernver}/extra" "${_extramodules##*/}"
  rmdir "${_kernver}"

  # compress kernel modules
  find "${pkgdir}" -name "*.ko" -exec gzip -9 {} +

  # load module on startup
  echo tp_smapi > "${srcdir}/${_pkgname}.conf"
  install -Dm644 "${srcdir}/${_pkgname}.conf" "${pkgdir}/usr/lib/modules-load.d/${pkgname}.conf"

  # update kernel version in install file
  sed -ri "s#^(extramodules=).*\$#\1${_extramodules}#" "${startdir}/${_pkgname}.install"
}
