# Maintainer: Jesus Alvarez <jeezusjr at gmail dot com>
# Contributor: Kyle Fuller <inbox at kylefuller dot co dot uk>

pkgbase=linux-rt-manjaro-spl_zfs
pkgname=('linux-rt-manjaro-spl' 'linux-rt-manjaro-zfs')
pkgver=0.6.5.7
pkgrel=1
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=('linux-rt-manjaro')
makedepends=("spl-dkms>=$pkgver" "zfs-dkms>=$pkgver"
             'dkms'
             'linux-rt-manjaro' 'linux-rt-manjaro-headers')
groups=('linux-rt-manjaro-extramodules')
install=install

prepare() {
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
}

build() {
  _extraver=extramodules-4.6-rt-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"

  # build host modules
  msg2 'Build modules'
  dkms --dkmsframework dkms.conf build "spl/$pkgver" -k "$_kernver" --verbose
  dkms --dkmsframework dkms.conf build "zfs/$pkgver" -k "$_kernver" --verbose
}

package_linux-rt-manjaro-zfs(){
  _extraver=extramodules-4.6-rt-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=zfs
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("$_pkgname=$pkgver")
  depends=('linux-rt-manjaro-spl' "zfs-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/zfs/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}

package_linux-rt-manjaro-spl(){
  _extraver=extramodules-4.6-rt-MANJARO
  _kernver="$(cat /usr/lib/modules/${_extraver}/version)"
  _pkgname=spl
  pkgdesc='Solaris Porting Layer kernel modules.'
  provides=("$_pkgname=$pkgver")
  depends=('kmod' 'linux-rt-manjaro' "spl-utils=${pkgver}")

  install -dm755 "$pkgdir/usr/lib/modules/$_extraver"
  cd "dkms/spl/$pkgver/$_kernver/$CARCH/module"
  install -m644 * "$pkgdir/usr/lib/modules/$_extraver"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extraver'/" "$startdir/install"
}
